gawk 'BEGIN{
FS=" -> "
Sx=500;Sy=0;
g[Sy,Sx]=2
minx=maxx=Sx;
miny=maxy=Sy;
}
function pp(  x, y)
{
  print numS
  for (y = miny; y <= maxy; y++) {
    printf("%3d ", y);
    for (x = minx; x <= maxx; x++) {
      printf("%c", g[y,x] == 1 ? "#" : g[y,x] == 2 ? "s" : g[y,x] == 3 ? "o" : ".");
    }
    print ""
  }
  print ""
}
{
  for (i=1;i<=NF;i++) {
    split($i,a,",");
    y = a[2]; x = a[1];
    g[y,x]=1
    if (x<minx)minx=x;
    if (x>maxx)maxx=x;
    if (y<miny)miny=y;
    if (y>maxy)maxy=y;
    if (i > 1) {
      if (x == lx) {
        #print "loop y", ly, y, "step", (ly > y ? -1 : 1)
        for (yi = ly; yi != y; yi += (ly > y ? -1 : 1)) {
          g[yi,x]=1;
        }
      } else if (y == ly) {
        #print "loop x", lx, x, "step", (lx > x ? -1 : 1)
        for (xi = lx; xi != x; xi += (lx > x ? -1 : 1)) {
          g[y,xi]=1;
        }
      }
    }
    lx=x
    ly=y
  }
}
function drop()
{
  while (and(g[ry+1,rx],3)==0) ry++;
}
END {
  FLy = maxy + 2;
  minx -= 2*maxy;
  maxx += 2*maxy;
  for (xi = minx; xi <= maxx; xi++) {
     g[FLy,xi]=1
  }
  #pp();
  full = 0;
  while (!full) {
    rx=Sx;ry=Sy;
    if (and(g[ry,rx],3) != 2) { break; }
    canmove = 1
    while (canmove) {
      drop();
      if (and(g[ry+1,rx-1],3)==0) {
        rx--;
        continue;
      } else if (and(g[ry+1,rx+1],3)==0) {
        rx++;
        continue
      } else {
        g[ry,rx]=3;
        numS++;
        #print numS
        break;
      }
    }
    #pp();
  }
  print numS
  #pp();
}' $1
